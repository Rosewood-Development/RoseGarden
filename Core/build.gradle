import org.apache.tools.ant.filters.ReplaceTokens

dependencies {
    // Referenced dependencies
    compileOnly "io.papermc.paper:paper-api:$spigotVersion-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.3'

    // Shaded dependencies
    api 'org.bstats:bstats-bukkit:3.0.2'
    api 'com.zaxxer:HikariCP:4.0.3'
    api 'org.slf4j:slf4j-api:2.0.13'
    api 'org.slf4j:slf4j-nop:2.0.13'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testCompileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.21:3.106.0'
}

tasks.register('processSource', Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [version: version])
    into "${layout.buildDirectory.get()}/src"
}

compileJava {
    source = processSource.outputs
}

shadowJar {
    archiveClassifier.set(null)

    relocate('org.bstats', 'dev.rosewood.rosegarden.lib.bstats')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            artifactId = 'rosegarden'
            project.shadow.component(publication)
        }
    }
}

build.dependsOn shadowJar
publishShadowPublicationToMavenRepository.dependsOn jar

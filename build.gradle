import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'jacoco'
}

group = 'dev.rosewood'
version = '1.3.0.22-DEV-SNAPSHOT'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven {
        name = "devmart-other"
        url = "https://nexuslite.gcnt.net/repos/other/"
    }
}

dependencies {
    // Referenced dependencies
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'org.jetbrains:annotations:24.0.1'

    // Shaded dependencies
    api 'org.bstats:bstats-bukkit:3.0.2'
    api 'com.zaxxer:HikariCP:5.0.1'
    api 'org.slf4j:slf4j-api:2.0.7'
    api 'org.slf4j:slf4j-nop:2.0.7'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.0'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testCompileOnly 'io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.9.0'

    // Other dependencies
    implementation "com.tcoded:FoliaLib:0.3.2"
}

tasks.register('processSource', Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [version: version])
    into "$buildDir/src"
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
    source = processSource.outputs
}

shadowJar {
    archiveClassifier.set(null)

    relocate('org.intellij', 'dev.rosewood.rosegarden.lib.intellij')
    relocate('org.jetbrains', 'dev.rosewood.rosegarden.lib.jetbrains')
    relocate('com.zaxxer.hikari', 'dev.rosewood.rosegarden.lib.hikaricp')
    relocate('org.slf4j', 'dev.rosewood.rosegarden.lib.slf4j')
    relocate('org.bstats', 'dev.rosewood.rosegarden.lib.bstats')
    relocate('com.tcoded.FoliaLib', 'dev.rosewood.rosegarden.lib.folialib')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'rosegarden'
            pom {
                name = 'rosegarden'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
//publishShadowPublicationToMavenRepository.dependsOn jar
